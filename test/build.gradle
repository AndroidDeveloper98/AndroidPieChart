plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'jacoco'
}

def kotlinVersion = rootProject.properties["kotlinVersion"]
def composeVersion = "1.0.2"

android {
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        debug.java.srcDirs += 'src/debug/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
    }

    packagingOptions {
        exclude 'META-INF/LICENSE*'
        exclude 'META-INF/*.kotlin_module'
    }

    buildFeatures {
        // Enables Jetpack Compose for this module
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion composeVersion
    }

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 30
        compileSdkVersion 30
        versionCode 1
        versionName '1.0'
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        testInstrumentationRunnerArgument 'runnerBuilder', 'de.mannodermaus.junit5.AndroidJUnit5Builder'
        consumerProguardFiles 'consumer-rules.pro'
    }

    signingConfigs {
        release {
            storeFile file(project.findProperty('release.storeFilePath') ?: "$rootProject.projectDir/${System.getenv('RELEASE_STORE_FILE_PATH')}")
            keyAlias project.findProperty('release.keyAlias') ?: System.getenv('RELEASE_KEY_ALIAS')
            keyPassword project.findProperty('release.keyPassword') ?: System.getenv('RELEASE_KEY_PASSWORD')
            storePassword project.findProperty('release.sotrePassword') ?: System.getenv('RELEASE_STORE_PASSWORD')

            // Optional, specify signing versions used
            v1SigningEnabled true
            v2SigningEnabled true
        }
    }

    buildTypes {
        debug {
            testCoverageEnabled true
        }
        release {
            signingConfig signingConfigs.release
            testCoverageEnabled true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = '11'
    }
}

jacoco {
    toolVersion = jacocoVersion
}

apply from: "${rootProject.projectDir}/scripts/configure-jacoco.gradle.kts"

tasks.withType(Test) {
    jacoco.includeNoLocationClasses = true
    jacoco.excludes = ['jdk.internal.*']
}

dependencies {
    // Integration with activities
    implementation 'androidx.activity:activity-compose:1.3.1'
    // Compose Material Design
    implementation "androidx.compose.material:material:$composeVersion"
    // Animations
    implementation "androidx.compose.animation:animation:$composeVersion"
    // Tooling support (Previews, etc.)
    implementation "androidx.compose.ui:ui-tooling:$composeVersion"
    // Integration with ViewModels
    implementation 'androidx.lifecycle:lifecycle-viewmodel-compose:1.0.0-alpha07'
    // UI Tests (or use androidx.compose.ui:ui-test)
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$composeVersion"


    implementation project(':piechart')
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.3.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.7.1'
    testImplementation 'org.assertj:assertj-core:3.19.0'
    androidTestImplementation 'org.junit.jupiter:junit-jupiter:5.7.1'
    androidTestImplementation 'androidx.test.ext:junit-ktx:1.1.2'
    androidTestImplementation 'androidx.test:runner:1.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    androidTestImplementation 'androidx.test.uiautomator:uiautomator:2.2.0'
    androidTestImplementation 'org.assertj:assertj-core:3.19.0'
    androidTestImplementation 'de.mannodermaus.junit5:android-test-core:1.2.2'
    androidTestRuntimeOnly 'de.mannodermaus.junit5:android-test-runner:1.2.2'
}
